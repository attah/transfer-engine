###################################################################
# transferengine plugin feature
###################################################################

!contains(DEFINES, BUILD_LIBTRANSFERENGINE) {

    CONFIG += qt plugin
    QT += network

    LIBTRANSFERENGINE_INC_DIR = $$[QT_INSTALL_PREFIX]/include/TransferEngine-qt5

    INCLUDEPATH += $${LIBTRANSFERENGINE_INC_DIR}
    DEPENDPATH  += $${LIBTRANSFERENGINE_INC_DIR}

    # We need to include these for MOC.
    HEADERS += $${INCLUDEPATH}/transferplugininterface.h
    HEADERS += $${INCLUDEPATH}/mediatransferinterface.h

    LIBTRANSFERENGINE_LIB_DIR = $$[QT_INSTALL_LIBS]
    unix: QMAKE_FLAGS += $${QMAKE_LFLAGS_RPATH}$${LIBTRANSFERENGINE_LIB_DIR}
    LIBS += -L$${LIBTRANSFERENGINE_LIB_DIR}
    qtAddLibrary(nemotransferengine-qt5)
}



